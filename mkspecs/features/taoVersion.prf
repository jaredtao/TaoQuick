!exists(TaoVersionDefined) {
    TaoVersionDefined=1

    TAO_VERSION_TAG = 1.0

    # current commit hash
    REVISION=$$system("git rev-parse HEAD")
    REVISION=$$str_member($${REVISION}, 0, $$num_add(10, -1))
    isEmpty(REVISION) {
        REVISION = 0000000000
    }

    # last tag
    Ver=$$system("git describe --abbrev=0 --tags")
    isEmpty(Ver) {
        Ver=0.0.0
    }
    VerList =$$split(Ver, .)
    MAJ = $$take_first(VerList)
    MIN = $$take_first(VerList)
    PAT = $$take_first(VerList)
    VER_MAJ = $${MAJ}
    VER_MIN = $${MIN}
    VER_PAT = $${PAT}
    VERSION = $${Ver}

    DEFINES += TaoREVISION=$${REVISION}
    DEFINES += TaoREVISIONSTR=\"\\\"$${REVISION}\\\"\"
    DEFINES += TaoDATETIME=\"\\\"$${_DATE_}\\\"\"
    DEFINES += TaoCompilerVendor=\"\\\"$${QT_VERSION} $${QMAKE_PLATFORM} $${QMAKE_COMPILER} $${QMAKE_TARGET.arch}\\\"\"
    #    message($${DEFINES})
}
